(define (domain detypedmiconic)

(:predicates
    (origin ?person ?floor)
    (destin ?person ?floor)
    (above ?floor1 ?floor2)
    (boarded ?person)
    (served ?person)
    (lift-at ?floor)
    (passenger ?p)
    (floor ?f)
)

(:action board
  :parameters (?f ?p)
  :precondition (and (lift-at ?f)
                     (origin ?p ?f)
                     (floor ?f)
                     (passenger ?p)
                )
  :effect (and (boarded ?p)
          )
)

(:action depart
  :parameters (?f ?p)
  :precondition (and (lift-at ?f)
                     (destin ?p ?f)
                     (boarded ?p)
                     (floor ?f)
                     (passenger ?p)
                )
  :effect (and (not (boarded ?p))
               (served ?p)
          )
)

(:action up
  :parameters (?f1 ?f2)
  :precondition (and (lift-at ?f1)
                     (above ?f1 ?f2)
                     (floor ?f1)
                     (floor ?f2)
                )
  :effect (and (lift-at ?f2)
               (not (lift-at ?f1))
          )
)

(:action down
  :parameters (?f1 ?f2)
  :precondition (and (lift-at ?f1)
                     (above ?f2 ?f1)
                     (floor ?f1)
                     (floor ?f2)
                )
  :effect (and (lift-at ?f2)
               (not (lift-at ?f1))
          )
)
)
